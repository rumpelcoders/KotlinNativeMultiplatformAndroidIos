apply plugin: 'com.android.library'
apply plugin: 'kotlin-multiplatform'

ext.serialization_version = '0.6.1'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

kotlin {
    targets {
        fromPreset(presets.android, 'android')
        fromPreset(presets.iosArm64, 'iosArm64')
    }

    sourceSets {
        commonMain.dependencies {
            api 'org.jetbrains.kotlin:kotlin-stdlib-common'
            implementation("io.ktor:ktor-client-core:${rootProject.ext.ktor_version}")
            implementation("io.ktor:ktor-client-json-native:${rootProject.ext.ktor_version}")
            implementation("io.ktor:ktor-client-auth-native:${rootProject.ext.ktor_version}")
            implementation "io.ktor:ktor-auth:${rootProject.ext.ktor_version}"
        }

        androidMain.dependencies {
            api 'org.jetbrains.kotlin:kotlin-stdlib'
            api "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
            api "com.android.support:support-media-compat:${rootProject.ext.supportLibVersion}"
            implementation("io.ktor:ktor-client-android:${rootProject.ext.ktor_version}")
            implementation("io.ktor:ktor-client-gson:${rootProject.ext.ktor_version}")
        }
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

task packForXCode(type: Sync) {
    final File frameworkDir = new File(buildDir, "xcode-frameworks")
    final String mode = project.findProperty("XCODE_CONFIGURATION")?.toUpperCase() ?: 'DEBUG'

    inputs.property "mode", mode


    from { kotlin.targets.iOS.compilations.main.getBinary("FRAMEWORK", mode).parentFile }
    into frameworkDir

    doLast {
        new File(frameworkDir, 'gradlew').with {
            text = "#!/bin/bash\nexport 'JAVA_HOME=${System.getProperty("java.home")}'\ncd '${rootProject.rootDir}'\n./gradlew \$@\n"
            setExecutable(true)
        }
    }
}

tasks.build.dependsOn packForXCode
